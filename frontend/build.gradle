plugins {
    id 'kotlin2js' version '1.3.11'
}

group 'elpsikongro'
version '1.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"
    testCompile "org.jetbrains.kotlin:kotlin-test-js"
}

compileKotlin2Js {
//    kotlinOptions.moduleKind = "amd"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = 'always'
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${buildDir}/web"

    dependsOn classes
}

assemble.doLast() {
    copy {
        includeEmptyDirs = false
        from new File("build/resources/main")
        into "${buildDir}/web"
    }
}

assemble.dependsOn assembleWeb